apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.pajato.android.gamechat"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        manifestPlaceholders = [gitScheme:"$rootProject.ext.gitScheme",
                                gitApiKey:"$rootProject.ext.gitApiKey",
                                gitServerClientId:"$rootProject.ext.gitServerClientId",
                                gitServerWidgetUrl:"$rootProject.ext.gitServerWidgetUrl"]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            // Run code coverage reports by default on debug builds.
            testCoverageEnabled = true
        }
    }

    lintOptions {
        disable 'IconMissingDensityFolder', 'GoogleAppIndexingWarning', 'GradleDependency'
        abortOnError true
    }

}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha1'
    compile "com.android.support:design:$rootProject.ext.supportLibraryVersion"

    // Use Project Lombok at https://projectlombok.org to
    // significantly reduce boilerplate code.
    provided 'javax.annotation:jsr250-api:1.0'
    provided "org.projectlombok:lombok:$rootProject.ext.lombokVersion"
    apt "org.projectlombok:lombok:$rootProject.ext.lombokVersion"

    // Android Testing Support Library's runner and rules. In the
    // following, exclude the support annotations library to resolve a
    // transitive dependency issue between test and production
    // APKs. This is required since Espresso internally has a
    // dependency on support-annotations.
    androidTestCompile("com.android.support.test:runner:$rootProject.ext.runnerVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test:rules:$rootProject.ext.runnerVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    // Espresso UI Testing dependencies.
    androidTestCompile("com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$rootProject.ext.espressoVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
    }
}
