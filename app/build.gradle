apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'project-report'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.pajato.android.gamechat"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 5
        versionName "1.0-alpha"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            // Run code coverage reports by default on debug builds.
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled = true
        }
    }

    //noinspection GroovyMissingReturnStatement
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        disable 'IconMissingDensityFolder', 'GoogleAppIndexingWarning', 'GradleDependency'
        warning 'RestrictedApi', 'GradleCompatible'
        abortOnError true
    }

}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

dependencies {
    // Android
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.ext.supportLibraryVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha6'

    // Google
    compile "com.google.android.gms:play-services-ads:$rootProject.ext.gmsVersion"
    compile "com.google.android.gms:play-services-appinvite:$rootProject.ext.gmsVersion"
    compile "com.google.android.gms:play-services-auth:$rootProject.ext.gmsVersion"

    // Firebase
    compile "com.google.firebase:firebase-analytics:$rootProject.ext.gmsVersion"
    compile "com.google.firebase:firebase-auth:$rootProject.ext.gmsVersion"
    compile "com.google.firebase:firebase-database:$rootProject.ext.gmsVersion"
    compile "com.google.firebase:firebase-messaging:$rootProject.ext.gmsVersion"
    compile "com.firebase:firebase-client-android:2.4.0"

    // Firebase UI
    compile 'com.firebaseui:firebase-ui-auth:1.0.1'

    // External
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'org.greenrobot:eventbus:3.0.0'

    // Android Testing Support Library's runner and rules. In the
    // following, exclude the support annotations library to resolve a
    // transitive dependency issue between test and production
    // APKs. This is required since Espresso internally has a
    // dependency on support-annotations.
    androidTestCompile("com.android.support.test:runner:$rootProject.ext.runnerVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test:rules:$rootProject.ext.runnerVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    // Espresso UI testing dependencies.
    androidTestCompile("com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$rootProject.ext.espressoVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
    }
    androidTestCompile "com.android.support:support-annotations:$rootProject.ext.supportLibraryVersion"

    // UIAuotmator testing dependencies.
    androidTestCompile("com.android.support.test.uiautomator:uiautomator-v18:$rootProject.ext.uiautomatorVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

}

// Per Firebase instructions this comes last.
apply plugin: 'com.google.gms.google-services'
