apply plugin: 'com.android.application'
apply plugin: 'project-report'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.pajato.android.gamechat"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0-beta"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            useProguard false
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFile 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            shrinkResources true
            minifyEnabled true
            useProguard false
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-rules.pro'
            testCoverageEnabled = true
        }
    }

    //noinspection GroovyMissingReturnStatement
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/MANIFEST.MF'
    }

    lintOptions {
        abortOnError true
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

dependencies {
    // Android
    compile "com.android.support:cardview-v7:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:customtabs:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.ext.supportLibraryVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:multidex:1.0.1'

    // Google
    compile "com.google.android.gms:play-services-ads:$rootProject.ext.gmsVersion"
    compile "com.google.android.gms:play-services-appinvite:$rootProject.ext.gmsVersion"
    compile "com.google.android.gms:play-services-auth:$rootProject.ext.gmsVersion"

    // Firebase
    compile "com.google.firebase:firebase-analytics:$rootProject.ext.gmsVersion"
    compile "com.google.firebase:firebase-auth:$rootProject.ext.gmsVersion"
    compile "com.google.firebase:firebase-database:$rootProject.ext.gmsVersion"
    compile "com.google.firebase:firebase-messaging:$rootProject.ext.gmsVersion"
    compile "com.firebase:firebase-client-android:2.4.0"
    compile "com.google.firebase:firebase-invites:$rootProject.ext.gmsVersion"

    // Firebase UI
    compile "com.firebaseui:firebase-ui-auth:$rootProject.ext.firebaseAuthUIVersion"

    // External
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'org.greenrobot:eventbus:3.0.0'

    // Espresso UI testing dependencies.
    androidTestCompile("com.android.support:support-annotations:$rootProject.ext.supportLibraryVersion")
    androidTestCompile("com.android.support:support-v4:$rootProject.ext.supportLibraryVersion")
    androidTestCompile("com.android.support:recyclerview-v7:$rootProject.ext.supportLibraryVersion")
    androidTestCompile("com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion")
    androidTestCompile("com.android.support:design:$rootProject.ext.supportLibraryVersion")
    androidTestCompile("com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoVersion")
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$rootProject.ext.espressoVersion")

    // UIAutomator testing dependencies.
    androidTestCompile("com.android.support.test.uiautomator:uiautomator-v18:$rootProject.ext.uiautomatorVersion")
}

// Per Firebase instructions this comes last.
apply plugin: 'com.google.gms.google-services'
